apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = properties.getProperty("project.version")
group = properties.getProperty("project.group")

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = properties.getProperty("bintray.pkg.repo")
        name = project.name
        websiteUrl = properties.getProperty("bintray.pkg.siteUrl")
        vcsUrl = properties.getProperty("bintray.pkg.siteUrl") + project.name + ".git"
        licenses = ["Apache-2.0"]
        publish = true
    }
}
